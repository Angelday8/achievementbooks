buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

sourceSets {
    main {
        resources {
            srcDirs += project(":core").sourceSets.main.resources.srcDirs
        }
    }
}

version="${mod_version}"
minecraft {
    version = "${minecraft_version}-${forge_version}-${minecraft_version}"
    runDir = "eclipse"

    replace "@VERSION@", project.version
    replaceIn "AchievementBooksMod.java"
}

group= "com.stateshifterlabs.achievementbooks"
archivesBaseName = "achievementbooks"


if (project.property("release_type").equalsIgnoreCase("release")) {
    version = "${tag_name}-MC${minecraft_version}"
} else {
    if(project.hasProperty("build_number")) {
        version = "${mod_version}b${build_number}-MC${minecraft_version}"
    } else {
        version = "${mod_version}b${getVersionAppendage()}-MC${minecraft_version}"
    }
}

task cleanUpMinecraftRunClutter {
    doLast {
        delete "${rootDir}/classes"
        delete "${rootDir}/out"
    }
}

clean.dependsOn cleanUpMinecraftRunClutter

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version

    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}



idea { module { inheritOutputDirs = true } }
